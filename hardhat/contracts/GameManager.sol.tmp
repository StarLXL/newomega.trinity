// SPDX-License-Identifier: UNLICENSED
pragma solidity >=0.6.8 <0.9.0;
pragma experimental ABIEncoderV2;

import "./GameEngine.sol";

struct PlayerDefence {
    address player;
    bool isInitialised;
    uint16[] defenceSelection;
    uint16 commander;
    bytes32 name;
}

struct PlayerData {
    address player;
    bool isInitialised;
    uint32 wins;
    uint32 losses;
}

struct LeaderboardEntry {
    address player;
    bytes32 name;
    uint32 wins;
    uint32 losses;
}

contract GameManager {
    address public owner = msg.sender;
    GameEngine public gameEngine;

    constructor(GameEngine engine) {
        gameEngine = engine;
    }

    modifier restricted() {
        require(
            msg.sender == owner,
            "This function is restricted to the contract's owner"
        );
        _;
    }

    event FightComplete(
        address indexed attacker,
        address indexed defender,
        FightResult result
    );

    mapping(address => PlayerData) playerDataMapping;
    address[] playerData;
    mapping(address => PlayerDefence) playerDefenceMapping;
    address[] playerDefenders;

    function getAllDefenders() public view returns (PlayerDefence[] memory) {
        PlayerDefence[] memory ret = new PlayerDefence[](playerDefenders.length);

        for (uint i = 0; i < playerDefenders.length; i++) {
            ret[i] = playerDefenceMapping[playerDefenders[i]];
        }

        return ret;
    }

    function registerDefence(uint16[] memory defence, uint16 commander, bytes32 name) public {
        PlayerDefence memory newDefence = PlayerDefence({
            player: msg.sender,
            defenceSelection: defence,
            commander: commander,
            name: name,
            isInitialised: true
        });
        if (!playerDefenceMapping[msg.sender].isInitialised) {
            playerDefenders.push(msg.sender);
        }
        playerDefenceMapping[msg.sender] = newDefence;
    }

    function getOwnDefence() public view returns (PlayerDefence memory) {
        require(playerDefenceMapping[msg.sender].isInitialised, 'Defence not registered');
        return playerDefenceMapping[msg.sender];
    }

    function attack(address enemy, uint16[] memory selection, uint16 commander, bytes32 playerName) public {
        require(playerDefenceMapping[enemy].isInitialised, 'Can only attack a registered defence');

        FightResult memory result = gameEngine.fight(
            selection, playerDefenceMapping[enemy].defenceSelection,
            commander, playerDefenceMapping[enemy].commander);

        result.nameLhs = playerName;
        result.nameRhs = playerDefenceMapping[enemy].name;

        if (!playerDataMapping[msg.sender].isInitialised) {
            PlayerData memory newDataLhs = PlayerData({
                player: msg.sender,
                isInitialised: true,
                wins: 0,
                losses: 0
            });

            playerDataMapping[msg.sender] = newDataLhs;
            playerData.push(msg.sender);
        }

        if (!playerDataMapping[enemy].isInitialised) {
            PlayerData memory newDataRhs = PlayerData({
                player: enemy,
                isInitialised: true,
                wins: 0,
                losses: 0
            });

            playerDataMapping[enemy] = newDataRhs;
            playerData.push(enemy);
        }

        if (result.lhsDead) {
            playerDataMapping[enemy].wins++;
            playerDataMapping[msg.sender].losses++;
        } else if (result.rhsDead) {
            playerDataMapping[msg.sender].wins++;
            playerDataMapping[enemy].losses++;
        }

        emit FightComplete(msg.sender, enemy, result);
    }

    function getLeaderboard() public view returns (LeaderboardEntry[] memory) {
        LeaderboardEntry[] memory entries = new LeaderboardEntry[](playerData.length);

        for (uint i = 0; i < playerData.length; i++) {
            address playerAddress = playerData[i];

            entries[i] = LeaderboardEntry({
                player: playerAddress,
                name: playerDefenceMapping[playerAddress].name,
                wins: playerDataMapping[playerAddress].wins,
                losses: playerDataMapping[playerAddress].losses
            });
        }

        return entries;
    }
}
